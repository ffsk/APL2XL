 result←(test OLEController)filename;filename;XL;cole;p;xlsx;nms;cnt;dat;r;c
    ⍝. This function is not a dfn because of OLEClient strangness
    ⍝. WB.Close cannot be assigned which causes problems in dfns, causing the function to return early
 :Select ⊢p←'-64'~⍨⊃# ⎕WG'APLVersion'

 :Case 'Windows'
     'XL'⎕WC'OLEClient' 'Excel.Application'('InstanceMode' 'New')
     cole←⎕NEW CloseOLE XL      ⍝ Solves a problem where workbooks remain open in the background
     XL.Workbooks.Open⊂filename ⍝ Open the workbook
     WB←XL.ActiveWorkbook
     result←test WB             ⍝ Perform the test
     WB.Save                    ⍝ Solves an issue where Excel automatically modifies text files
     WB.Close ⍬                 ⍝ Close the workbook
     XL.Quit
     ⎕EX'XL' ⍝ ensure that XL is deleted

 :Case 'Linux'
     xlsx←#.Main.Platform.xlsx.open filename
     nms←#.Main.Platform.xlsx.get_sheet_names xlsx
     cnt←⊃⍴nms
     dat←#.Main.Platform.xlsx.get_sheet xlsx(⊃nms)0  ⍝ Assume there is only one worksheet
     r c←⍴dat ⋄ range1←'A1' ⋄ range2←(c⌷⎕A),(⍕r)

     (sheet←⎕NS'').(Name Count Ranges)←nms cnt(range1 range2)
     (wb←⎕NS'').(xlsx FileName Sheets ActiveSheet)←xlsx filename sheet dat

     result←test wb

 :Else
     ('Unsupported platform.')⎕SIGNAL 11  ⍝: "',p,'"
 :EndSelect
