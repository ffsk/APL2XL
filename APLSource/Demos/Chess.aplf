 Chess←{
     Setup←{
         bl←'♟♜♞♝♛♚'
         wh←'♙♖♘♗♕♔'
         back←{⊂{⍵,2↓⌽⍵}1↓⍵}
         pawns←{8⍴⊃⍵}¨bl wh
         ↑({⍵,⌽⍵}1 1 0 0)\(back bl),pawns,(back wh)
     }

     Export←{
         range←⎕NS''
         range.Value←⍵
         range.Address←'B2'
         range.Fill←¯1↓⍤1⊢8 9⍴('solid' 'd4b790' 1)('solid' '87673b' 1)

         ⍝. Border ←→ left right top bottom diagonal
         border←'lrtbd'

         ⍝. Map each index of the board to a bitmask related to border indices to toggle the border for that cell
         top←(⊂'tl'),(6⍴'t'),⊂'tr'
         body←(⊂'l'),(6⍴''),(⊂'r')
         bottom←(⊂'bl'),(6⍴'b'),(⊂'br')
         range.Border←↑1 6 1\{('none' 0)('thick' 1)[border∊⍵]}¨¨top body bottom

         (sheet←⎕NS'').(Name Ranges)←'board'range
         (wb←⎕NS'').(FileName Sheets)←((739⌶0),'/chess.xlsx')sheet
         ##.Main.Export wb
     }
     Export Setup''
 }
